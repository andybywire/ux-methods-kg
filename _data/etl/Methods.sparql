PREFIX : <https://www.uxmethods.org/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>

CONSTRUCT {
  ?URI a :Method;
    :produces ?productIRI;
    dc:description ?description;
    dc:source ?source;
    dc:publisher ?publisher;
    :shortDescription ?shortDescription;
    rdfs:label ?label;
    :steps ?steps;
    :providesInputFor ?inputForIRI.
}
FROM <https://docs.google.com/spreadsheets/d/19cAHQNoICjCjOOMDMgvBlRRY7c_L5EC0FG8pzqNLkzk/export?format=csv&id=19cAHQNoICjCjOOMDMgvBlRRY7c_L5EC0FG8pzqNLkzk&gid=1136175385>
WHERE {
  BIND (URI(CONCAT('https://www.uxmethods.org/', ?id)) AS ?URI)
  ?product apf:strSplit (?produces ",")
  BIND (REPLACE(?product, " ", "") AS ?productId)
  BIND (URI(CONCAT('https://www.uxmethods.org/', ?productId)) AS ?productIRI)
  OPTIONAL {
    ?input apf:strSplit (?inputFor ",")
    BIND (REPLACE(?input, " ", "") AS ?inputForId)
    BIND (URI(CONCAT('https://www.uxmethods.org/', ?inputForId)) AS ?inputForIRI)
  }
}
